{-
Approximates roots of a quadratic equation.
Input: coefficients a, b, c
Output: root count roots, roots r1, r2 if found
-}

{- x = (-b +- sqrt(b*b-4*a*c)/2a) -}
-- calculate the discriminant
discriminant := b*b - 4*a*c;
-- if it's negative...
if discriminant < 0 then
    -- there are no roots
    roots := 0
else
    -- if the discriminant is zero
    if discriminant = 0 then
        -- there's one root
        roots := 1;
        -- 0 is its own square root
        sqrt_discriminant := 0
    else
        -- two roots
        roots := 2;
        -- if the discriminant is positive, take its square root with the Babylonian method with fifty iterations
        n := 50;
        last_guess := 0;
        guess := 1;
        while n > 0 and not (last_guess = guess) do
            last_guess := guess;
            -- guess = (guess + discriminant / guess) / 2
            -- we want to calculate discriminant / guess right now
            quotient := 0;
            remainder := discriminant;
            while remainder >= guess do
                quotient := quotient + 1;
                remainder := remainder - guess
            od;
            -- quotient is now discriminant / guess, so calculate (guess + quotient) / 2
            remainder := guess + quotient;
            quotient := 0;
            while remainder >= 2 do
                quotient := quotient + 1;
                remainder := remainder - 2
            od;
            -- quotient is now the new guess
            guess := quotient
        od;
        -- store the final guess
        sqrt_discriminant := guess
    fi;
    -- we have sqrt(discriminant), now do (-b +- that) / 2a
    denominator := 2 * a;
    numerator1 := (0 - b) - sqrt_discriminant;
    -- do the division - counterintuitively, r1/r2 are quotient, not remainder, and also these can be negative
    r1 := 0;
    while numerator1 >= denominator do
        r1 := r1 + 1;
        numerator1 := numerator1 - denominator
    od;
    while numerator1 <= 0 - denominator do
        r1 := r1 - 1;
        numerator1 := numerator1 + denominator
    od;
    if roots > 1 then
        numerator2 := (0 - b) + sqrt_discriminant;
        r2 := 0;
        while numerator2 >= denominator do
            r2 := r2 + 1;
            numerator2 := numerator2 - denominator
        od;
        while numerator2 <= 0 - denominator do
            r2 := r2 - 1;
            numerator2 := numerator2 + denominator
        od
    else skip fi
fi
